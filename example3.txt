Msat  = 1000e3
Aex   = 10e-12
alpha = 3

// define exchange length
lex := sqrt(Aex.GetRegion(1) / (0.5 * mu0 * pow(Msat.GetRegion(1),2)))

d     := 30 * lex                        // we test for d/lex = 30
Sizex := 5*d                             // magnet size x
Sizey := 1*d
Sizez := 0.1*d

nx := pow(2, ilogb(Sizex / (0.75*lex)))  // power-of-two number of cells
ny := pow(2, ilogb(Sizey / (0.75*lex)))  // not larger than 0.75 exchange lengths

SetGridSize(nx, ny, 1)
SetCellSize(Sizex/nx, Sizey/ny, Sizez)

m = Uniform(1, 0.1, 0)                   // initial mag
Run(5e-9)                                // relax

save(m)                                  // remanent magnetization
m_ := average(m)                         
expect("mx", m_[0], 0.9627, 1e-3)        // test m against known value
expect("my", m_[1], 0.0756, 1e-3)
expect("mz", m_[2], 0, 1e-3)