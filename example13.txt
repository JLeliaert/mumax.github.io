// In this example we drive a domain wall in PMA material by spin-transfer torque.
// We set up a post-step function that makes the simulation box "follow" the domain
// wall. Like this, only a small number of cells is needed to simulate an infinitely
// long magnetic wire.

setgridsize(128, 128, 1)
setcellsize(2e-9, 2e-9, 1e-9)

msat    = 600e3
aex     = 10e-12
anisU   = vector(0, 0, 1)
Ku1     = 0.59e6
xi      = 0.2
alpha   = 3                                   // high damping for fast relax

m     = twoDomain(0, 0, 1, 1, 1, 0, 0, 0, -1) // up-down domains with wall between Bloch and Néél type
run(0.1e-9)                                   // relax
alpha = 0.02                                  // normal damping

// Set post-step function that centers simulation window on domain wall.
postStep(ext_centerPMAwall)

// Schedule output
autosave(m, 100e-12)

// Run for 1ns with current through the sample
jpol = vector(1.5e13, 0, 0)
run(.5e-9)